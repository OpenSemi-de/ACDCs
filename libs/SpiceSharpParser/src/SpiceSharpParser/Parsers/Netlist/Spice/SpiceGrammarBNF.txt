<NETLIST> ::= <TITLE> <NEW-LINE> <STATEMENTS> <NETLIST-ENDING> 
		| <NEW-LINE> <STATEMENTS> <NETLIST-ENDING>
<NETLIST-WITHOUT-TITLE> ::= <STATEMENTS> <NETLIST-ENDING>
<NETLIST-ENDING> ::= ".END" <EOF> 
		| ".END" <NEW-LINE> <NEW-LINES> <EOF>
		| <NEW-LINE> <NEW-LINES> <EOF> 
		| <EOF>
<NEW-LINES> ::= <NEW-LINE> <NEW-LINES> | <EMPTY>
<STATEMENTS> ::= <STATEMENT> <STATEMENTS> | <NEW-LINE> <STATEMENTS> | <EMPTY> 
<STATEMENT> ::= <COMPONENT> <NEW-LINE> 
		| <SUBCKT> <NEW-LINE> 
		| <MODEL> <NEW-LINE> 
		| <CONTROL> <NEW-LINE> 
		| <COMMENT-LINE> <NEW-LINE> 
<COMPONENT> ::= <WORD> <PARAMETERS>
<COMMENT-LINE> ::= <COMMENT>
<SUBCKT> ::= ".SUBCKT" <WORD> <PARAMETERS> <NEW-LINE> <STATEMENTS> <SUBCKT-ENDING>
			| ".SUBCKT" <IDENTIFIER> <PARAMETERS> <NEW-LINE> <STATEMENTS> <SUBCKT-ENDING>
<SUBCKT-ENDING> ::= ".ENDS" <WORD> | ".ENDS"
<MODEL> ::= ".MODEL" <WORD> <PARAMETERS>
<CONTROL> ::= "." <WORD> <PARAMETERS>
			| ".ENDL" <PARAMETERS>
			| ".IF" (<EXPRESSION>)
			| ".ELSE" (<EXPRESSION>)
			| ".ELSEIF" (<EXPRESSION>)
			| ".ENDIF"
<PARAMETERS> ::=  <PARAMETER> <COMMA>? <PARAMETERS> 
			 | <EMPTY> 
<PARAMETER> ::= <VECTOR>
			| <PARAMETER-EQUAL>
			| <PARAMETER-BRACKET>
			| <PARAMETER-SINGLE>
			| <EXPRESSION-EQUAL>
			| <POINT>
<EXPRESSION-EQUAL> ::= <EXPRESSION> "=" <POINTS>
					| <EXPRESSION> <POINTS>

<PARAMETER-EQUAL> ::= <PARAMETER-EQUAL-SINGLE> 
				  | <WORD> "=" <VECTOR>
				  | <IDENTIFIER> "=" <VECTOR>
				  | <WORD> "(" ")" "=" <PARAMETER-SINGLE>
				  | <WORD> "(" <PARAMETER-SINGLE> ")" "=" <PARAMETER-SINGLE>
   				  | <WORD> "(" <VECTOR> ")" "=" <PARAMETER-SINGLE>
				  | <IDENTIFIER> "(" <PARAMETER-SINGLE> ")" "=" <PARAMETER-SINGLE>
   				  | <IDENTIFIER> "(" <VECTOR> ")" "=" <PARAMETER-SINGLE>
<PARAMETER-EQUAL-SINGLE> :: = <WORD> "=" <PARAMETER-SINGLE>
				  | <IDENTIFIER> "=" <PARAMETER-SINGLE>
<PARAMETER-BRACKET> ::= <WORD> "(" <PARAMETER-BRACKET-CONTENT> ")"
					| <IDENTIFIER> "(" <PARAMETER-BRACKET-CONTENT> ")"
<PARAMETER-BRACKET-CONTENT> ::= <PARAMETERS>
<VECTOR> ::= <PARAMETER-SINGLE> "," <PARAMETER-SINGLE> <VECTOR-CONTINUE> 
<VECTOR-CONTINUE> ::= "," <PARAMETER-SINGLE> <VECTOR-CONTINUE> | <EMPTY>
<PARAMETER-SINGLE> ::= <WORD> | <VALUE> | <STRING> | <IDENTIFIER> | <REFERENCE> | <EXPRESSION> | <PERCENT> 

<POINT> ::= "(" <POINT-VALUES> ")"
<POINT-VALUES>  ::= <POINT-VALUE> 
				| <POINT-VALUE> "," <POINT-VALUES>
				| <POINT-VALUE> <POINT-VALUES>
<POINT-VALUE> ::= <VALUE> | <EXPRESSION> | <WORD> | <IDENTIFIER>
<POINTS> ::= <POINT> <POINTS-CONTINUE>
			| "(" <POINT> <POINT-CONTINUE> ")"
<POINTS-CONTINUE> ::= <POINT> <POINTS-CONTINUE> | <EMPTY>

<EMPTY> ::= ""
<NEW-LINE> ::= "\r\n" | "\n" | "\r"
<EOF> ::= "$"